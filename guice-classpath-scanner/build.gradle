apply plugin: 'maven'
apply plugin: 'signing'


description = 'guice-classpath-scanner'
dependencies {
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            pom.groupId = 'com.richodemus.scanner'
            pom.artifactId = 'guice-classpath-scanner'
            pom.version = '1.0.0'

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Guice Classpath Scanner'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A simple classpath scanner for Guice'
                url 'https://github.com/RichoDemus/guice-classpath-scanner'

                scm {
                    connection 'scm:git:https://github.com/RichoDemus/guice-classpath-scanner'
                    developerConnection 'scm:git:https://github.com/RichoDemus/guice-classpath-scanner'
                    url 'https://github.com/RichoDemus/guice-classpath-scanner'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'richodemus'
                        name 'Richard Tjerngren'
                        email 'richodemus@gmail.com'
                    }
                }
            }
        }
    }
}
